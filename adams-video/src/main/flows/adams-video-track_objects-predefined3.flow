# Project: adams
# Date: 2016-02-15 15:09:27
# User: fracpete
# Charset: UTF-8
adams.flow.control.Flow -annotation "Tracks an object throughout a movie using a predefined, initial location.\\nSaves and displays the generated trail object." -flow-execution-listener adams.flow.execution.NullListener
 adams.flow.standalone.TabView -width 1000 -x -3
  adams.flow.sink.SpreadSheetDisplay -name Data -short-title true -writer adams.data.io.output.NullWriter
  adams.flow.sink.TrailDisplay -name Trail -short-title true -writer adams.gui.print.NullWriter -paintlet adams.gui.visualization.trail.paintlet.Circles
 adams.flow.standalone.CallableActors
  adams.flow.sink.ImageViewer -name Frame -short-title true -writer adams.gui.print.NullWriter -zoom -1.0 -selection-processor adams.gui.visualization.image.selection.NullProcessor -image-overlay "adams.gui.visualization.image.QuadrilateralLocationsOverlayFromReport -regexp Tracker\\\\.Current"
  adams.flow.source.StorageValue -name background -storage-name image
 adams.flow.source.Start
 adams.flow.control.Trigger -name "load initial position"
  adams.flow.source.FileSupplier -file ${FLOWS}/data/track_peter_rect.txt
  adams.flow.transformer.TextFileReader -reader adams.data.io.input.SingleStringTextReader
  adams.flow.transformer.StringTrim
  adams.flow.transformer.SetVariable -var-name pos
 adams.flow.control.Trigger -name track
  adams.flow.source.FileSupplier -file ${FLOWS}/data/track_peter.mjpeg
  adams.flow.control.Tee -name id
   adams.flow.transformer.BaseName -remove-extensions true
   adams.flow.transformer.SetVariable -var-name id
  adams.flow.transformer.MjpegImageSequence -output-type FRAME
  adams.flow.control.Sleep -interval 10
  adams.flow.transformer.SetStorageValue -storage-name image
  adams.flow.control.Once -name "init position"
   adams.flow.transformer.SetReportValue -field Tracker.Init[S] -value @{pos}
  adams.flow.control.Once -name "create new trail"
   adams.flow.control.Tee -name width
    adams.flow.transformer.ImageInfo
    adams.flow.transformer.SetVariable -var-name width
   adams.flow.control.Tee -name height
    adams.flow.transformer.ImageInfo -type HEIGHT
    adams.flow.transformer.SetVariable -var-name height
   adams.flow.control.Trigger -name "new trail"
    adams.flow.source.NewTrail -id @{id} -width @{width} -height @{height}
    adams.flow.transformer.AddTrailBackground -background background
    adams.flow.transformer.SetStorageValue -storage-name trail
  adams.flow.transformer.TrackObjects -algorithm adams.flow.transformer.objecttracker.BoofCVCirculant
  adams.flow.control.Tee -name "get location"
   adams.flow.transformer.GetReportValue -field Tracker.Current[S]
   adams.flow.transformer.SetVariable -var-name current
   adams.flow.transformer.Convert -conversion "adams.data.conversion.MultiConversion -conversion adams.data.conversion.StringToQuadrilateralLocation -conversion adams.data.conversion.QuadrilateralLocationCenter -conversion adams.data.conversion.Point2DToString"
   adams.flow.transformer.StringSplit -expression " "
   adams.flow.control.Tee -name x
    adams.flow.transformer.GetArrayElement
    adams.flow.transformer.SetVariable -var-name x
   adams.flow.control.Tee -name y
    adams.flow.transformer.GetArrayElement -index 2
    adams.flow.transformer.SetVariable -var-name y
   adams.flow.control.Trigger -name "add step"
    adams.flow.source.StorageValue -storage-name trail
    adams.flow.transformer.AddTrailStep -x @{x} -y @{y}
    adams.flow.transformer.SetStorageValue -storage-name trail
  adams.flow.control.Branch
   adams.flow.control.Trigger -name image
    adams.flow.source.StorageValue -storage-name image
    adams.flow.transformer.SetReportValue -field Tracker.Current[S] -value @{current}
    adams.flow.control.Cast -classname adams.data.image.AbstractImageContainer
    adams.flow.sink.CallableSink -callable Frame
 adams.flow.control.Trigger -name "display trail"
  adams.flow.source.StorageValue -storage-name trail
  adams.flow.control.Branch
   adams.flow.control.Sequence -name data
    adams.flow.transformer.Convert -conversion adams.data.conversion.TrailToSpreadSheet
    adams.flow.sink.CallableSink -callable Data
   adams.flow.sink.CallableSink -callable Trail
   adams.flow.transformer.TrailFileWriter -writer adams.data.io.output.SimpleTrailWriter -dir ${FLOWS}/output -file-name-generation ID
